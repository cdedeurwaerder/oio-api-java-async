apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8
	
repositories {
  mavenCentral()
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.10'
}

group = 'io.openio.sds'
version = '0.1.0'

dependencies {
  compile group: 'org.asynchttpclient', name: 'async-http-client', version: '2.0.0-RC5'
  compile group: 'com.google.guava', name: 'guava', version: '18.0'
  compile group: 'com.google.code.gson', name: 'gson', version: '2.3.1'
  compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.2'
  compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.2'
  testCompile group: 'junit', name: 'junit', version: '[4.0)'
}

test {
  exclude 'io/openio/sds/client/**'
}

jar {
  baseName='openio-api'
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allJava
}

task fatJar(type: Jar) {
  baseName='openio-api'
  classifier = 'all'
  from {
    configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
  } {
       exclude "META-INF/*.SF"
       exclude "META-INF/*.DSA"
       exclude "META-INF/*.RSA"
  }
  with jar
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshosts/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'OpenIO SDS API'
        packaging 'jar'
        description 'OpenIO SDS API'
        url 'http://openio.io'

        scm {
          url 'https://github.com/open-io/oio-api-java'
        }
        licenses {
          license {
            name 'GNU Lesser General Public License v3'
            url 'https://www.gnu.org/licenses/lgpl.txt'
          }
        }
        developers {
          developer {
            id 'jkasarherou'
            name 'Julien Kasarherou'
            email 'julien@openio.io'
          }
        }
      }
    }
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      groupId 'io.openio.sds'
      artifactId 'openio-api'

      artifact sourcesJar {
        classifier "sources"
      }
    }
  }
}
